create procedure [dbo].[JobsExecuting]
as
begin

 declare @JobsExecuting table
 (
  job_id uniqueidentifier null,
  last_run_date nvarchar (20) null,
  last_run_time nvarchar (20) null,
  next_run_date nvarchar (20) null,
  next_run_time nvarchar (20) null,
  next_run_schedule_id int null,
  requested_to_run int null,
  request_source int null,
  request_source_id sysname collate database_default null,
  running int null,
  current_step int null,
  current_retry_attempt int null,
  job_state int null
 )

 declare @job_owner sysname
 declare @is_sysadmin int
 set @is_sysadmin = isnull (is_srvrolemember ('sysadmin'), 0)
 set @job_owner = suser_sname ()

 insert into @JobsExecuting
 execute master.dbo.xp_sqlagent_enum_jobs @is_sysadmin, @job_owner

 if @@rowcount = 0
 begin
  insert @JobsExecuting
  values (null,null,null,null,null,null,null,null,null,null,null,null,null)
 end


 update @JobsExecuting
 set last_run_time = right ('000000' + last_run_time, 6),
  next_run_time = right ('000000' + next_run_time, 6)

 select 
  j.name as JobName, 
  j.enabled as 'Enabled',
  case x.running 
   when 1 then 'Running' 
   else 
    case h.run_status 
     when 2 then 'Inactive' 
     when 4 then 'Inactive' 
     else 'Completed' 
    end
   end as CurrentStatus,
  coalesce (x.current_step, 0) as CurrentStep,
  case when x.last_run_date > 0
   then
    convert (datetime, substring (x.last_run_date, 1, 4) + '-' + substring (x.last_run_date, 5, 2)
    + '-' + substring (x.last_run_date, 7, 2) + ' ' + substring (x.last_run_time, 1, 2) + ':'
    + substring (x.last_run_time, 3, 2) + ':' + substring (x.last_run_time, 5, 2) + '.000', 121)
   else null
  end as LastRunTime,
  case h.run_status 
   when 0 then 'Fail' 
   when 1 then 'Success' 
   when 2 then 'Retry' 
   when 3 then 'Cancel' 
   when 4 then 'In Progress' 
  end as LastRunOutcome,
  case 
   when h.run_duration > 0 then
    (h.run_duration / 1000000) * (3600 * 24) + (h.run_duration / 10000 % 100) * 3600
    + (h.run_duration / 100 % 100) * 60 + (h.run_duration % 100)
   else null
   end as LastRunDuration
 from @JobsExecuting x
  left join msdb.dbo.sysjobs j on x.job_id = j.job_id
  left outer join msdb.dbo.syscategories c on j.category_id = c.category_id
  left outer join msdb.dbo.sysjobhistory h on x.job_id = h.job_id and x.last_run_date = h.run_date and x.last_run_time = h.run_time and h.step_id = 0
 where x.running = 1
 order by jobname

end
